#version 450
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_nonuniform_qualifier : require
#extension GL_EXT_buffer_reference : require
#extension GL_EXT_scalar_block_layout : require

#include "include/mesh.glsl"
#include "include/scene.glsl"
#include "include/indirect.glsl"

layout(std430, buffer_reference) readonly buffer MeshInfoBuffer
{
    MeshInfo infos[];
};

layout(std430, buffer_reference, buffer_reference_align = 8) readonly buffer SceneInstanceBuffer
{
    Instance instances[];
};

layout(std430, buffer_reference, buffer_reference_align = 8) readonly buffer ScenePrimitiveInstanceBuffer
{
    PrimitiveInstance primitives[];
};

layout(std430, buffer_reference, buffer_reference_align = 8) buffer IndirectDrawCountsBuffer
{
    uint counts[];
};

layout(std430, buffer_reference, buffer_reference_align = 8) writeonly buffer IndirectCommandInfosBuffer
{
    DrawIndexedIndirectCommandInfo cmds[];
};

layout(push_constant) uniform PushConstants
{
    uint indirect_draw_count_index;
    IndirectDrawCountsBuffer indirect_draw_counts_ptr;

    MeshInfoBuffer mesh_infos_ptr;
    SceneInstanceBuffer scene_instances_ptr;
    ScenePrimitiveInstanceBuffer scene_primitives_ptr;

    IndirectCommandInfosBuffer indirect_command_infos_ptr;
} push_constants;

void main()
{
    const uint scene_primitives_index = gl_GlobalInvocationID.x;
    const PrimitiveInstance scene_primitive = push_constants.scene_primitives_ptr.primitives[scene_primitives_index];

    const Instance scene_instance = push_constants.scene_instances_ptr.instances[scene_primitive.instance_index];

    if (scene_instance.visable == 0) {
        return;
    }

    MeshInfo mesh = push_constants.mesh_infos_ptr.infos[scene_instance.mesh_index];
    PrimitiveInfo mesh_primitive = mesh.primitives.p[scene_primitive.primitive_index];

    //TODO: futher culling

    const uint indirect_draw_count_index = push_constants.indirect_draw_count_index;
    uint command_index = atomicAdd(push_constants.indirect_draw_counts_ptr.counts[indirect_draw_count_index], 1);

    DrawIndexedIndirectCommandInfo cmd;
    cmd.indexCount = mesh_primitive.index_count;
    cmd.instanceCount = 1;
    cmd.firstIndex =  mesh.index_offset + mesh_primitive.index_offset;
    cmd.vertexOffset = int(mesh.vertex_offset + mesh_primitive.vertex_offset);
    cmd.firstInstance = command_index;

    cmd.instance_index =  scene_primitive.instance_index;
    cmd.material_index = scene_primitive.material_instance_index;

    push_constants.indirect_command_infos_ptr.cmds[command_index] = cmd;
}
